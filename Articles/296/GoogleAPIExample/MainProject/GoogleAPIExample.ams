## ams_version=1.0

Model Main_GoogleApiArticle {
    Set sAddresses {
        Index: iOrigin, iDestination;
        webui::ElementTextIdentifier: spOrigin;
    }
    StringParameter spOrigin {
        IndexDomain: iOrigin;
    }
    StringParameter spDestination {
        IndexDomain: iDestination;
    }
    Parameter pDuration {
        IndexDomain: (iOrigin, iDestination);
    }
    Parameter pDistance {
        IndexDomain: (iOrigin, iDestination);
    }
    Procedure GetDistances {
        Body: {
            !APIkey := "YOUR_API_KEY";
            responseFile:="Output.xml";
            ListOrigins:="Eiff Tower, Paris | The atomium is in Brussels | AIMMS Haarlem | Roma Italy Coliseum | The alhambra, granada spain";
            ListDestinations:="Eiffel Tower, Pari | The atomium, Brussels | AIMMS Haarlem | Coliseum, Roma Italy | The alhambra, granada spain";
            !Set the values for the parameters we want to use
            requestparameters := { 'key': APIkey, 'origins': ListOrigins, 'destinations' : ListDestinations };
            !Set the request URL
            web::query_format(requestparameters,formattedparameters);
            URL:="https://maps.googleapis.com/maps/api/distancematrix/xml?"+formattedparameters;
            !Create and submit the request
            web::request_create(requestId);
            web::request_setMethod(requestId,"GET");
            web::request_setURL(requestId,URL);
            web::request_setResponseBody(requestId,'File',responseFile);
            web::request_invoke(requestId,responsecode);
            !Import from the response XML file
            READXML(responseFile,"GoogleDistances.axm");
        }
        DeclarationSection Request {
            Parameter responsecode;
            StringParameter requestId;
            Set TotalParameters {
                Index: tp;
                InitialData: {
                    DATA{origins,destinations,address,key,mode,language,region,avoid,units,arrival_time,departure_time,traffic_model,transit_mode,transit_routing_preference};
                }
            }
            StringParameter requestparameters {
                IndexDomain: tp;
            }
            StringParameter APIkey;
            StringParameter ListOrigins;
            StringParameter ListDestinations;
            StringParameter URL;
            StringParameter responseFile;
            StringParameter formattedparameters;
        }
    }
}
